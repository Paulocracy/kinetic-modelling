function rate_law_one_s_one_p(E_conc, k_cat, conc_S, conc_P, k_S, k_P, dG0, R, T)
  (E_conc * k_cat * (conc_S / k_S) * (1 - ((conc_P/conc_S)/(exp(-dG0/(R*T))))) ) / (1 + (conc_S/k_S) + (conc_P/k_P))
end

function rate_law_two_s_one_p(E_conc, k_cat, conc_S1, conc_S2, conc_P, k_S1, k_S2, k_P, dG0, R, T)
  (E_conc * k_cat * (conc_S1 / k_S1) * (conc_S2 / k_S2) * (1 - ((conc_P/(conc_S1*conc_S2))/(exp(-dG0/(R*T))))) ) / (1 + (conc_S1/k_S1) + (conc_S2/k_S2) + (conc_P/k_P))
end

function rate_law_one_s_two_p(E_conc, k_cat, conc_S, conc_P1, conc_P2, k_S, k_P1, k_P2, dG0, R, T)
  (E_conc * k_cat * (conc_S / k_S) * (1 - (((conc_P1*conc_P2)/conc_S)/(exp(-dG0/(R*T))))) ) / (1 + (conc_S/k_S) + (conc_P1/k_P1) + (conc_P2/k_P2))
end

model pathway()
    # Constant definitions
    ## General
    R := 0.008315
    T := 298
    ## R1
    E_conc_R1 := 1
    k_cat_R1 := 6
    k_Sex_R1 := 0.18
    k_S_R1 := 2.5
    dG0_R1 := 0
    ## R2
    E_conc_R2 := 1
    k_cat_R2 := 5
    k_S_R2 := 0.4
    k_X_R2 := 2.1
    k_A_R2 := 3.1
    dG0_R2 := 4
    ## R3
    E_conc_R3 := 1
    k_cat_R3 := 8
    k_A_R3 := 4.6
    k_X_R3 := 0.4
    k_B_R3 := 2.5
    dG0_R3 := -5
    ## R4
    E_conc_R4 := 1
    k_cat_R4 := 9
    k_B_R4 := 0.5
    k_C_R4 := 0.9
    k_X_R4 := 3.1
    dG0_R4 := -5
    ## R5
    E_conc_R5 := 1
    k_cat_R5 := 10
    k_C_R5 := 1.5
    k_X_R5 := 2.6
    k_P_R5 := 6.8
    dG0_R5 := 4
    ## R6
    E_conc_R6 := 1
    k_cat_R6 := 24
    k_P_R6 := 0.01
    k_Pex_R6 := 14
    dG0_R6 := 0
    ## RTB
    E_conc_RTB := 1
    k_cat_RTB := 100000
    k_B_RTB := 2
    k_Bex_RTB := 2
    dG0_RTB := 0


    # Metabolite initialization
    const substanceOnly species Sex = 4
    substanceOnly species S = 1e-5
    substanceOnly species A = 1e-5
    substanceOnly species B = 1e-5
    substanceOnly species C = 1e-5
    substanceOnly species X = 1e-5
    substanceOnly species P = 1e-5
    substanceOnly species Pex = 1e-5
    substanceOnly species Bex = 1e-5


    # Reactions
    ## Sex uptake
    Sex_uptake: -> $Sex; Sex
    ## R1
    R1: $Sex -> S;
    R1 = rate_law_one_s_one_p(E_conc_R1, k_cat_R1, Sex, S, k_Sex_R1, k_S_R1, dG0_R1, R, T)
    ## R2
    R2: S -> A + X;
    R2 = rate_law_one_s_two_p(E_conc_R2, k_cat_R2, S, A, X, k_S_R2, k_A_R2, k_X_R2, dG0_R2, R, T)
    ## R3
    R3: A + X -> B;
    R3 = rate_law_two_s_one_p(E_conc_R3, k_cat_R3, A, X, B, k_A_R3, k_X_R3, k_B_R3, dG0_R3, R, T)
    ## R4
    R4: B -> C + X;
    R4 = rate_law_one_s_two_p(E_conc_R4, k_cat_R4, B, C, X, k_B_R4, k_C_R4, k_X_R4, dG0_R4, R, T)
    ## R5
    R5: C + X -> P;
    R5 = rate_law_two_s_one_p(E_conc_R5, k_cat_R5, C, X, P, k_C_R5, k_X_R5, k_P_R5, dG0_R5, R, T)
    ## R6
    R6: P -> Pex;
    R6 = rate_law_one_s_one_p(E_conc_R6, k_cat_R6, P, Pex, k_P_R6, k_Pex_R6, dG0_R6, R, T)
    ## Pex excretion
    Pex_uptake: Pex ->; Pex
    ## RTB
    RTB: B -> Bex;
    RTB = rate_law_one_s_one_p(E_conc_RTB, k_cat_RTB, B, Bex, k_B_RTB, k_Bex_RTB, dG0_RTB, R, T)
end
